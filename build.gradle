plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.6'
}

group = 'io.github.ssoloff'
version = '5.3.0'
sourceCompatibility = '1.7'

ext {
    gradleVersion = '2.11'
}

repositories {
    mavenCentral()

    maven {
        url 'https://dl.bintray.com/ssoloff/maven/'
    }
}

dependencies {
    compile(
        [group: project.group, name: 'jedit', version: project.version],
        [group: 'junit', name: 'junit', version: '4.12'],
        [group: 'org.easytesting', name: 'fest-swing', version: '1.2']
    )
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
    description = 'Generates Gradle wrapper files.'
    group = 'Build Setup'

    gradleVersion = project.gradleVersion
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    dryRun = bintrayDryRun.toBoolean()
    publish = true
    publications = ['maven']

    pkg {
        repo = 'maven'
        name = 'io.github.ssoloff:jedit-test-framework'
        desc = 'jEdit test framework'
        websiteUrl = 'https://github.com/ssoloff/jedit-test-framework'
        issueTrackerUrl = 'https://github.com/ssoloff/jedit-test-framework/issues'
        vcsUrl = 'https://github.com/ssoloff/jedit-test-framework.git'
        licenses = ['GPL-2.0']
        publicDownloadNumbers = true

        version {
            name = "${project.version}"
            desc = "jEdit Test Framework ${project.version}"
        }
    }
}

javadoc {
    options {
        links = ['http://junit.org/javadoc/latest/']
        use = true
        windowTitle = 'jEdit Test Framework API'
    }
}
